plugins {
    id 'java'
    id 'maven-publish'
    id 'com.moowork.node' version '1.3.1'
    id 'com.enonic.xp.base' version '2.0.0'
    id "com.jfrog.bintray" version "1.8.4"
}

dependencies {
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-websocket:${xpVersion}"
    include 'no.item.xp:lib-xp-session:1.0.0'
}

repositories {
    mavenLocal()
    jcenter()
    xp.enonicRepo()
    maven {
        url  "http://itemconsulting.bintray.com/public"
    }
}

node {
    download = true
    version '10.16.0'
}

task npmInstallForce( type: NpmTask ) {
    description = 'Update all project node dependencies'
    args = ['install', '--force']
}

task cleanNode( type: org.gradle.api.tasks.Delete ) {
    delete 'node_modules'
}

task serverWebpack( type: NodeTask, dependsOn: npmInstall ) {
    environment = [ 'NODE_ENV': nodeEnvironment() ]
    args = [ '--config', 'webpack.server.config.js', '--color', '--hide-modules' ]
    description = 'Build server JS.'
    inputs.dir 'src/main'
    outputs.dir "${buildDir}/resources/main"
    script = file( 'node_modules/webpack-cli/bin/cli.js' )
}

processResources {
    include '**/*'
    exclude '**/.gitkeep'
    exclude '**/tsconfig.*.json'
    exclude '**/*.es'
    exclude '**/*.es6'
    exclude '**/*.ts'
    includeEmptyDirs false
}

jar {
    exclude 'assets/styles/**/*.js'

    dependsOn += serverWebpack
}

jar.outputs.dir "${buildDir}/resources/main"

def nodeEnvironment() {
    def environments = [ prod : 'production', dev: 'development' ]
    def nodeEnv = environments[hasProperty( 'env' ) ? env : 'prod']
    return nodeEnv != null ? nodeEnv : 'production'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId group
            artifactId projectName
            version version
        }
    }
}

bintray {
    user = "$bintrayUser"
    key = "$bintrayApiKey"
    publications = ['mavenJava']
    configurations = ['archives']
    pkg {
        repo = 'public'
        name = 'no.item.xp.lib-xp-turbo'
        userOrg = 'itemconsulting'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/ItemConsulting/lib-xp-turbo.git'
        version {
            name = project.version
            released = new Date()
        }
    }
}
