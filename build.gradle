plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.node-gradle.node' version '2.2.3'
    id 'com.enonic.xp.base' version '2.0.0'
    id "com.jfrog.bintray" version "1.8.4"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    compile "com.enonic.xp:portal-api:${xpVersion}"
}

repositories {
    mavenLocal()
    jcenter()
    xp.enonicRepo()
    maven {
        url  "https://dl.bintray.com/itemconsulting/public"
    }
}

node {
    download = true
    version = '12.13.1'
}

task npmInstallForce( type: NpmTask ) {
    description = 'Update all project node dependencies'
    args = ['install', '--force']
}

task cleanNode( type: org.gradle.api.tasks.Delete ) {
    delete 'node_modules'
}

task serverWebpack( type: NodeTask, dependsOn: npmInstall ) {
    environment = [ 'NODE_ENV': nodeEnvironment() ]
    // args = [ '--config', 'webpack.server.config.babel.js', '--color', '--hide-modules' ]
    args = [ '--config', 'webpack.server.config.js', '--color', '--hide-modules' ]
    description = 'Build server JS.'
    inputs.dir 'src/main'
    outputs.dir "${buildDir}/resources/main"
    script = file( 'node_modules/webpack-cli/bin/cli.js' )
}

processResources {
    include '**/*'
    exclude '**/.gitkeep'
    exclude '**/tsconfig.*.json'
    exclude '**/*.es'
    exclude '**/*.es6'
    exclude '**/*.ts'
    includeEmptyDirs false
}

jar {
    exclude 'assets/styles/**/*.js'

    dependsOn += serverWebpack
}

jar.outputs.dir "${buildDir}/resources/main"

def nodeEnvironment() {
    def environments = [ prod : 'production', dev: 'development' ]
    def nodeEnv = environments[hasProperty( 'env' ) ? env : 'prod']
    return nodeEnv != null ? nodeEnv : 'production'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId group
            artifactId projectName
            version version
        }
    }
}

bintray {
    user = "$bintrayUser"
    key = "$bintrayApiKey"
    publications = ['mavenJava']
    configurations = ['archives']
    pkg {
        repo = 'public'
        name = 'no.item.xp.lib-xp-turbo'
        userOrg = 'itemconsulting'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/ItemConsulting/lib-xp-turbo.git'
        version {
            name = project.version
            released = new Date()
        }
    }
}
